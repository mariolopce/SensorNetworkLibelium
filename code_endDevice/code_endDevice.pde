/*
    ------ Program for the end device --------

    Explanation: This program obtains the sensor values of temperature,
 *  humidity, pressure and accelerometer, and its battery level
 *  each 30 seconds the measurements are performed and the values are 
 *  sent using IEEE 802.15.4. Moreover, alarms detecting free fall or 
 *  movement by accelerometer and PIR sensor, respectively, will be
 *  sent to the gateway immediately after being detected
*/

// Libraries
#include <WaspSensorEvent_v30.h>
#include <WaspXBee802.h>
#include <WaspFrame.h>

// Destination MAC address
//////////////////////////////////////////
char RX_ADDRESS[] = "0013A200417EE503";   //address of the gateway
//////////////////////////////////////////

// Define the Waspmote ID
char WASPMOTE_ID[] = "node_01";

// define variables
uint8_t error;
char buffer[100];

// Variables used to transform the measurements from float into string
////////////////////////////////////////////////////////////////////////
char temp_string[10]; 
char x_string[10];
char y_string[10];
char z_string[10];
char preasure_string[10];
char humidity_string[10];
////////////////////////////////////////////////////////////////////////

// Variables used to save the measurements performed by the end device
////////////////////////////////////////////////////////////////////////
uint8_t status;
int x_acc;
int y_acc;
int z_acc;
float temp;
float humd;
float pres;
int value;
////////////////////////////////////////////////////////////////////////

// Define the socket of the movement sensor
pirSensorClass pir(SOCKET_1);

void setup()
{
  // Setting ON USB and ACC
  USB.ON();
  USB.println(F("Exercise 3: Sensor reading + sleep"));
  ACC.ON();

  // store Waspmote identifier in EEPROM memory
  frame.setID( WASPMOTE_ID );
  
  // init XBee
  xbee802.ON();
  
  // Setting time
  RTC.ON(); 
  RTC.setTime("29:01:22:02:18:24:56");
  USB.print(F("Time: "));
  USB.println(RTC.getTime());
  
  // Setting alarm  in offset mode:
  // Alarm 1 is set 30 seconds later
  RTC.setAlarm1("00:00:00:30",RTC_OFFSET,RTC_ALM1_MODE5);
}


void loop()
{
    // Turn on the sensor board
    Events.ON();
    // Read the value of the PIR sensor
    value = pir.readPirSensor();

    // Wait until PIR sensor is stabilized.
    while (value == 1)
    {
      USB.println(F("...wait for PIR stabilization"));
      delay(1000);
      value = pir.readPirSensor();
    }
    
    // Enable interruptions from the board
  Events.attachInt();
  
  ///////////////////////////////////////////////
  // 1. Starts accelerometer
  ///////////////////////////////////////////////
  ACC.ON();

  ///////////////////////////////////////////////
  // 2. Enable interruption: ACC Free Fall interruption 
  ///////////////////////////////////////////////
  ACC.setFF(); 

  //----------Check Register-----------------------
  // should always answer 0x32, it is used to check
  // the proper functionality of the accelerometer
  status = ACC.check();

  ///////////////////////////////////////////////
  // 3. Set low-power consumption state
  ///////////////////////////////////////////////  
  USB.println(F("Waspmote goes into sleep mode until the Accelerometer causes an interrupt"));
  PWR.sleep(SENSOR_ON);


   // Interruption event happened

  ///////////////////////////////////////////////
  // 4. ACC module has to be set to ON again
  //    Disable interruption: ACC Free Fall interrupt 
  //    This is done to avoid new interruptions
  ///////////////////////////////////////////////
  ACC.ON();
  ACC.unsetFF(); 
  // Disable interruptions from the board
  Events.detachInt();

  USB.ON();
  USB.println(F("Waspmote wakes up"));

  ///////////////////////////////////////////////
  // 5. Check the interruption source 
  //
  // Check if the interruption generated is from the accelerometer
  ////////////////////////////////////////////////////////////////////////////
  if( intFlag & ACC_INT )
  {
    // clear interruption flag
    intFlag &= ~(ACC_INT);
    
    xbee802.ON();   // Xbee module has to be set to ON again due to the sleep
    snprintf(buffer, sizeof(buffer), "Alarm: Free fall detected");  // Send message warning of the alarm to the gateway

    // send XBee packet
    error = xbee802.send( RX_ADDRESS, (uint8_t*)buffer, strlen(buffer));
    
    // check TX flag
    if( error == 0 )
    {
      USB.println(F("send ok"));
      
    }
    else 
    {
      USB.println(F("send error"));
      
    }

    memset(buffer, 0, sizeof(buffer)); // Reset buffer contents to 0
    
    // print information about the interruption
    USB.ON();
    USB.println(F("++++++++++++++++++++++++++++"));
    USB.println(F("++ ACC interrupt detected ++"));
    USB.println(F("++++++++++++++++++++++++++++")); 
    USB.println(); 
    
  }
    ////////////////////////////////////////////////////////////////////////////
  // Check if the interruption is generated by the RTC
  ////////////////////////////////////////////////////////////////////////////
  if ( intFlag & RTC_INT )
  {
    // clear interruption flag
    intFlag &= ~(RTC_INT);
    // Measuring stars, switch on green LED
    Utils.setLED(LED1, LED_ON);
    
    // print information about the interruption
    USB.ON();
    USB.println(F("++++++++++++++++++++++++++++"));
    USB.println(F("++ RTC interrupt detected ++"));
    USB.println(F("++++++++++++++++++++++++++++")); 
    USB.println();

    //Switch on the events board
    Events.ON();
    //Get temperature
    temp = Events.getTemperature();
    //Get humidity
    humd = Events.getHumidity();
    //Get pressure
    pres = Events.getPressure();

    //----------X Value-----------------------
    x_acc = ACC.getX();
  
    //----------Y Value-----------------------
    y_acc = ACC.getY();
  
    //----------Z Value-----------------------
    z_acc = ACC.getZ();
  
    //-------------------------------
  
    USB.print(F("\n------------------------------\nCheck: 0x")); 
    USB.println(status, HEX);
    USB.println(RTC.getTime());
    USB.println(F("\n \t0X\t0Y\t0Z")); 
    USB.print(F(" ACC\t")); 
    USB.print(x_acc, DEC);
    USB.print(F("\t")); 
    USB.print(y_acc, DEC);
    USB.print(F("\t")); 
    USB.println(z_acc, DEC);
  
    
    ///////////////////////////////////////
    // Print BME280 Values
    ///////////////////////////////////////
    USB.println("-----------------------------");
    USB.print("Temperature: ");
    USB.printFloat(temp, 2);
    USB.println(F(" Celsius"));
    USB.print("Humidity: ");
    USB.printFloat(humd, 1); 
    USB.println(F(" %")); 
    USB.print("Pressure: ");
    USB.printFloat(pres, 2); 
    USB.println(F(" Pa")); 
    USB.println("-----------------------------");  

    ///////////////////////////////////////
    // Show battery level
    ///////////////////////////////////////
    USB.print(F("Battery Level: "));
    USB.print(PWR.getBatteryLevel(),DEC);
    USB.println(F(" %"));
    USB.println("-----------------------------"); 
    USB.println("");

    // Measuring finishes, set green LED to OFF
    Utils.setLED(LED1, LED_OFF);

    USB.OFF();

    // Set alarm to cause an interruption in 30 seconds
    RTC.setAlarm1("00:00:00:30",RTC_OFFSET,RTC_ALM1_MODE5);
    // Switch on the xbee module 
    xbee802.ON();

    ///////////////////////////////////////
    // Transform the float measurement values into strings
    ///////////////////////////////////////
    dtostrf(temp, 6, 2, temp_string);
    dtostrf(x_acc, 6, 2, x_string);
    dtostrf(y_acc, 6, 2, y_string);
    dtostrf(z_acc, 6, 2, z_string);
    dtostrf(humd, 6, 2, humidity_string);
    dtostrf(pres, 6, 2, preasure_string);

    // Transmission starts, red LED ON
    Utils.setLED(LED0, LED_ON);
    
     // create new frame
    frame.createFrame(ASCII);  
    // add frame fields
    frame.addSensor(SENSOR_STR, "new_sensor_frame");
    frame.addSensor(SENSOR_BAT, PWR.getBatteryLevel()); 

    /////////////////////////////////////////////////////////////////////////////////
    // Data sent: temperature, humidity, pressure, battery, x-axis, y-axis and z-axis
    /////////////////////////////////////////////////////////////////////////////////
    snprintf(buffer, sizeof(buffer), "%s %s %s %d %s %s %s", temp_string, humidity_string, preasure_string, PWR.getBatteryLevel(), x_string, y_string, z_string);
    // send XBee packet
    error = xbee802.send( RX_ADDRESS, (uint8_t*)buffer, strlen(buffer));
    
    // check TX flag
    if( error == 0 )
    {
      USB.println(F("send ok"));
      
    }
    else 
    {
      USB.println(F("send error"));
      

    }

    memset(buffer, 0, sizeof(buffer));  // Reset buffer contents to 0
    Utils.setLED(LED0, LED_OFF);        // Transmission finished, red LED OFF
    
  }
////////////////////////////////////////////////////////////////////////////
  // Check if the interruption is generated by the PIR sensor
  ////////////////////////////////////////////////////////////////////////////
  if (intFlag & SENS_INT)
  {
    // Load the interruption flag. Contains information about what device is triggering the interruption
    Events.loadInt();
    
    // In case the interruption came from PIR
    if (pir.getInt())
    {
      xbee802.ON();   // Switch ON the xbee module
      snprintf(buffer, sizeof(buffer), "Alarm: Motion detected"); // Send message warning of the alarm to the gateway

      // send XBee packet
      error = xbee802.send( RX_ADDRESS, (uint8_t*)buffer, strlen(buffer));
      
      // check TX flag
      if( error == 0 )
      {
        USB.println(F("send ok"));
        
      }
      else 
      {
        USB.println(F("send error"));

      }

      memset(buffer, 0, sizeof(buffer)); // Resets buffer contents to 0
      
      USB.println(F("-----------------------------"));
      USB.println(F("Interruption from PIR"));
      USB.println(F("-----------------------------"));
    }    
    
    
    
    // Clean the interruption flag
    intFlag &= ~(SENS_INT);
    
  }

  ///////////////////////////////////////////////////////////////////////
  // 6. Clear interruption pin   
  ///////////////////////////////////////////////////////////////////////
  // This function is used to make sure the interruption pin is cleared
  // if a non-captured interruption has been produced
  PWR.clearInterruptionPin();
  

}
